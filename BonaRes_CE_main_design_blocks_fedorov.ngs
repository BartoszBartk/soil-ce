Design
? Bayesian efficient design
? 2 alternatives per choice set defined here (3rd alternative will be constant base alternative).
? 5 attributes (b1 drought protection, b2 flood protection, b3 carbon sequestration, b4 water supply/quality, b5 cost).


;alts = alt1*, alt2*                        ?Unlabeled alternatives, dominant alternatives removed
;rows = 240                                 ?Specifies the number of choice situations to be generated
;block = 30                                 ?Specifies the number of blocks
;eff = (mnl,d,mean)+0.01*(imbalance)
;alg = mfederov                             ?Modified Fedorov algorithm applied (see also below)

;model:                                     ? specifying Bayesian priors in the form:(n,mean,stdev)
U(alt1) = b1[(n,0.0099,0.0572)]*drought[80,90,100]  +  b2[(n,0.0121,0.0566)]*flood[80,90,100] + b3[(n,0.0193,0.0369)]*climate[75,100] + b4[(n,0.0208,0.0235)]*water[50,75,100] + b5[(n,-0.013,0.0120)]*price[25,50,75,100,125,150] /
U(alt2) = b1*drought + b2*flood + b3*climate + b4*water + b5*price
$



? Regarding the question if 0.1-1 or 10-100% should be the attribute value:
? The Fisher information matrix depends not only on the prior parameter values (which you have implicitly assumed all equal to 0), but also depends on the attribute levels, see for example Huber and Zwerina (1996). The wider you choose 
? the levels, the statistically more efficient your design, as level range has a positive effect on the Fisher information (since trade-offs are larger). So in a D-efficient design you ALWAYS have to specify the 'real' attribute levels 
? that you present to respondents and that you use to estimate your model. Only in case of dummy or effects coding these attribute levels are irrelevant. In case of orthogonal designs, attribute levels are again irrelevant.

? The standard algorithm (swapping algorithm) struggles with the creation of the blocks. Maximum 30 blocks are possible. The alternative is the modified Fedorov algorithm which is applied here.
? Yet, the modified Fedorov algortihm does not preserve attribute level balance.
